#lang racket

(define *op-table* (make-hash))
(define (put op type proc) (hash-set! *op-table* (list op type) proc))
(define (get op type) (hash-ref *op-table* (list op type) '()))

(define (attach-tag type-tag contents) (cons type-tag contents))
(define (type-tag datum) (car datum))
(define (contents datum) (cdr datum))

(define tower
  '((integer 1)
    (rational 2)
    (real 3)
    (complex 4)))

(define (tower-index x)
  (define (iter tail)
    (if (null? tail)
        -1
        (let ((i (car tail)))
          (if (eq? (car i) x)
              (car (cdr i))
              (iter (cdr tail))))))
  (iter tower))

(define (max-type types)
  (define (iter guess tail)
    (if (null? tail)
        guess
        (if (> (tower-index guess) (tower-index (car tail)))
            (iter guess (cdr tail))
            (iter (car tail) (cdr tail)))))
  (iter (car types) (cdr types)))

(define (raise-to type x)
  (if (eq? (type-tag x) type)
      x
      (raise-to type (raise x))))

(define (raise-args type args)
  (map (lambda(x) (raise-to type x)) args))

(define (apply-generic op . args)  
  (define (apply-generic-internal local-args)
    (let ((type-tags (map type-tag local-args)))
      (let ((proc (get op type-tags)))
        (if (null? proc)
            false
            (apply proc (map contents local-args))))))
  (let ((result1 (apply-generic-internal args)))
    (if result1
        result1
        (let ((types (remove-duplicates (map type-tag args))))
          (print (cons op args))
          (newline)
          (if (> (length types) 1)              
              (let ((result2 (apply-generic-internal (raise-args (max-type types) args))))
                (if result2
                    result2
                    (error "No method for these types")))
              (error "No method for these types"))))))

(define (raise z) (apply-generic 'raise z))
(define (real-part z) (apply-generic 'real-part z))
(define (imag-part z) (apply-generic 'imag-part z))
(define (magnitude z) (apply-generic 'magnitude z))
(define (angle z) (apply-generic 'angle z))
(define (add x y) (apply-generic 'add x y))
(define (sub x y) (apply-generic 'sub x y))
(define (mul x y) (apply-generic 'mul x y))
(define (div x y) (apply-generic 'div x y))
(define (since z) (apply-generic 'since z))
(define (cosine z) (apply-generic 'cosine z))
(define (arctan x y) (apply-generic 'arctan x y))
(define (exp x y) (apply-generic 'exp x y))
(define (square x) (mul x x))

(define (make-integer x) ((get 'make 'integer) x))
(define (make-rational n d) ((get 'make 'rational) n d))
(define (make-real x) ((get 'make 'real) x))
(define (make-complex-from-real-imag x y) ((get 'make-from-real-imag 'complex) x y))
(define (make-complex-from-mag-ang r a) ((get 'make-from-mag-ang 'complex) r a))

(define (install-integer-package)
  (define (tag x) (attach-tag 'integer x))
  (put 'make 'integer (lambda (x) (tag x)))
  (put 'add '(integer integer) (lambda (x y) (tag (+ x y))))
  (put 'sub '(integer integer) (lambda (x y) (tag (- x y))))
  (put 'mul '(integer integer) (lambda (x y) (tag (* x y))))
  (put 'div '(integer integer) (lambda (x y) (make-real (/ x y))))
  (put 'since '(integer) (lambda (x) (make-real (sin x))))
  (put 'cosine '(integer) (lambda (x) (make-real (cos x))))
  (put 'arctan '(integer reintegeral) (lambda (x y) (make-real (atan x y))))
  (put 'exp '(integer integer) (lambda (x y) (make-real (expt x y))))
  (put 'raise '(integer) (lambda (x) (make-rational x 1)))
  )

(define (install-rational-package)
  (define (gcd a b)
    (if (= a 0)
        b
        (gcd (remainder b a) a)))
  (define (make-rat n d)
    (let ((g (gcd n d)))
      (cons (/ n g) (/ d g))))
  (define numer car)
  (define denom cdr)
  (define (ratio x) (/ (numer x) (denom x)))
  
  (define (add-rat x y)
    (make-rat (+ (* (numer x) (denom y))
                 (* (numer y) (denom x)))
              (* (denom x) (denom y))))
  (define (sub-rat x y)
    (make-rat (- (* (numer x) (denom y))
                 (* (numer y) (denom x)))
              (* (denom x) (denom y))))
  (define (mul-rat x y)
    (make-rat (* (numer x) (numer y))
              (* (denom x) (denom y))))
  (define (div-rat x y)
    (make-rat (* (numer x) (denom y))
              (* (denom x) (numer y))))
    
  (define (tag x) (attach-tag 'rational x))
  (put 'make 'rational (lambda (n d) (tag (make-rat n d))))
  (put 'add '(rational rational) (lambda (x y) (tag (add-rat x y))))
  (put 'sub '(rational rational) (lambda (x y) (tag (sub-rat x y))))
  (put 'mul '(rational rational) (lambda (x y) (tag (mul-rat x y))))
  (put 'div '(rational rational) (lambda (x y) (tag (div-rat x y))))
  (put 'since '(rational) (lambda (x) (make-real (sin (/ (numer x) (denom x))))))
  (put 'cosine '(rational) (lambda (x) (make-real (cos (/ (numer x) (denom x))))))
  (put 'arctan '(rational rational) (lambda (x y) (make-real (atan (ratio x) (ratio y)))))
  (put 'exp '(rational rational) (lambda (x y) (make-real (expt (ratio x) (ratio y)))))
  (put 'raise '(rational) (lambda (x) (make-real (/ (numer x) (denom x)))))
  )

(define (install-real-package)
  (define (tag x) (attach-tag 'real x))
  (put 'make 'real (lambda (x) (tag x)))
  (put 'add '(real real) (lambda (x y) (tag (+ x y))))
  (put 'sub '(real real) (lambda (x y) (tag (- x y))))
  (put 'mul '(real real) (lambda (x y) (tag (* x y))))
  (put 'div '(real real) (lambda (x y) (tag (/ x y))))
  (put 'since '(real) (lambda (x) (tag (sin x))))
  (put 'cosine '(real) (lambda (x) (tag (cos x))))
  (put 'arctan '(real real) (lambda (x y) (tag (atan x y))))
  (put 'exp '(real real) (lambda (x y) (tag (expt x y))))
  (put 'raise '(real) (lambda (x) (make-complex-from-real-imag x 0)))
  )

(define (install-rectangular-package)
  (define (real-part z) (car z))
  (define (imag-part z) (cdr z))
  (define (make-from-real-imag x y) (cons x y))
  (define (magnitude z) (exp (add (square (real-part z)) (square (imag-part z))) (make-real 0.5)))
  (define (angle z) (arctan (imag-part z) (real-part z)))
  (define (make-from-mag-ang r a) (cons (mul r (cosine a)) (mul r (since a))))

  (define (tag x) (attach-tag 'rectangular x))
  (put 'make-from-real-imag 'rectangular (lambda (x y) (tag (make-from-real-imag x y))))
  (put 'make-from-mag-ang 'rectangular (lambda (r a) (tag (make-from-mag-ang r a))))
  (put 'real-part '(rectangular) real-part)
  (put 'imag-part '(rectangular) imag-part)
  (put 'magnitude '(rectangular) magnitude)
  (put 'angle '(rectangular) angle)  
  )

(define (install-polar-package)
  (define (magnitude z) (car z))
  (define (angle z) (cdr z))
  (define (make-from-mag-ang r a) (cons r a))
  (define (real-part z) (mul (magnitude z) (cosine (angle z))))
  (define (imag-part z) (mul (magnitude z) (since (angle z))))
  (define (make-from-real-imag x y) (cons (sqrt (add (mul x x) (mul y y))) (arctan y x)))
  
  (define (tag x) (attach-tag 'polar x))
  (put 'make-from-real-imag 'polar (lambda (x y) (tag (make-from-real-imag x y))))
  (put 'make-from-mag-ang 'polar (lambda (r a) (tag (make-from-mag-ang r a))))
  (put 'real-part '(polar) real-part)
  (put 'imag-part '(polar) imag-part)
  (put 'magnitude '(polar) magnitude)
  (put 'angle '(polar) angle)  
  )

(define (install-complex-package)
  (install-rectangular-package)
  (install-polar-package)
  
  (define (make-from-real-imag x y) ((get 'make-from-real-imag 'rectangular) x y))
  (define (make-from-mag-ang r a) ((get 'make-from-mag-ang 'polar) r a))

  (define (add-complex z1 z2)
    (make-from-real-imag (add (real-part z1) (real-part z2))
                         (add (imag-part z1) (imag-part z2))))
  (define (sub-complex z1 z2)
    (make-from-real-imag (sub (real-part z1) (real-part z2))
                         (sub (imag-part z1) (imag-part z2))))
  (define (mul-complex z1 z2)
    (make-from-mag-ang (mul (magnitude z1) (magnitude z2))
                       (add (angle z1) (angle z2))))
  (define (div-complex z1 z2)
    (make-from-mag-ang (div (magnitude z1) (magnitude z2))
                       (sub (angle z1) (angle z2))))
  
  (define (tag z) (attach-tag 'complex z))
  (put 'make-from-real-imag 'complex (lambda (x y) (tag (make-from-real-imag x y))))
  (put 'make-from-mag-ang 'complex (lambda (x y) (tag (make-from-mag-ang x y))))
  (put 'add '(complex complex) (lambda (z1 z2) (tag (add-complex z1 z2))))
  (put 'sub '(complex complex) (lambda (z1 z2) (tag (sub-complex z1 z2))))
  (put 'mul '(complex complex) (lambda (z1 z2) (tag (mul-complex z1 z2))))
  (put 'div '(complex complex) (lambda (z1 z2) (tag (div-complex z1 z2))))
  (put 'real-part '(complex) real-part)
  (put 'imag-part '(complex) imag-part)
  (put 'magnitude '(complex) magnitude)
  (put 'angle '(complex) angle)
  )

(install-integer-package)
(install-real-package)
(install-rational-package)
(install-complex-package)

(div
 (mul
  (sub
   (add
    (make-complex-from-real-imag (make-integer 1) (make-real 4))
    (make-complex-from-mag-ang (make-rational 2 3) (make-integer 1)))
   (make-complex-from-real-imag (make-rational 2 3) (make-rational 2 3)))
  (make-complex-from-mag-ang (make-real 4) (make-rational 2 3)))
 (make-complex-from-real-imag (make-real 4) (make-integer 1)))